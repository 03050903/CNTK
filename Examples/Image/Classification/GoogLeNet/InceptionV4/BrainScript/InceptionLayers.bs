#
# Inception-V4 network components
# Details are in https://arxiv.org/pdf/1602.07261v2.pdf
#

ConvBNReLULayer {outChannels, kernel, stride, pad, bnTimeConst} = Sequential(
    ConvolutionalLayer{outChannels, kernel, init = 'heNormal', stride = stride, pad = pad, bias = false} :
    BatchNormalizationLayer{spatialRank = 2, normalizationTimeConstant = bnTimeConst} :
    ReLU
)

#
# Figure 4 from https://arxiv.org/pdf/1602.07261v2.pdf
#
InceptionA {bnTimeConst} = {
    apply(x) = {
        # 1x1 Convolution
        branch1x1 = ConvBNReLULayer{96, (1:1), (1:1), true, bnTimeConst}(x)

        # 3x3 Convolution
        branch3x3 = Sequential( 
            ConvBNReLULayer{64, (1:1), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{96, (3:3), (1:1), true, bnTimeConst}
        ) (x)

        # Double 3x3 Convolution
        branch3x3dbl = Sequential(
            ConvBNReLULayer{64, (1:1), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{96, (3:3), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{96, (3:3), (1:1), true, bnTimeConst}
        ) (x)

        # Average Pooling
        branch_pool = Sequential(
            AveragePoolingLayer{(3:3), stride = (1:1), pad = true} :
            ConvBNReLULayer{96, (1:1), (1:1), true, bnTimeConst}
        ) (x)

        out = Splice((branch1x1:branch3x3:branch3x3dbl:branch_pool), axis=3)
    }.out
}.apply

#
# Figure 5 from https://arxiv.org/pdf/1602.07261v2.pdf
#
InceptionB {bnTimeConst} = {
    apply(x) = {
        # 1x1 Convolution
        branch1x1 = ConvBNReLULayer{384, (1:1), (1:1), true, bnTimeConst}(x)

        # 7x7 Convolution
        branch7x7 = Sequential( 
            ConvBNReLULayer{192, (1:1), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{224, (1:7), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{256, (7:1), (1:1), true, bnTimeConst}
        ) (x)

        # Double 3x3 Convolution
        branch7x7dbl = Sequential(
            ConvBNReLULayer{192, (1:1), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{192, (1:7), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{224, (7:1), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{224, (1:7), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{256, (7:1), (1:1), true, bnTimeConst}
        ) (x)

        # Average Pooling
        branch_pool = Sequential(
            AveragePoolingLayer{(3:3), stride = (1:1), pad = true} :
            ConvBNReLULayer{128, (1:1), (1:1), true, bnTimeConst}
        ) (x)

        out = Splice((branch1x1:branch7x7:branch7x7dbl:branch_pool), axis=3)
    }.out
}.apply

#
# Figure 6 from https://arxiv.org/pdf/1602.07261v2.pdf
#
InceptionC {bnTimeConst} = {
    apply(x) = {
        # 1x1 Convolution
        branch1x1 = ConvBNReLULayer{256, (1:1), (1:1), true, bnTimeConst}(x)

        # 3x3 Convolution
        branch3x3_i = ConvBNReLULayer{384, (1:1), (1:1), true, bnTimeConst}(x)
        branch3x3_1 = ConvBNReLULayer{256, (1:3), (1:1), true, bnTimeConst}(branch3x3_i)
        branch3x3_2 = ConvBNReLULayer{256, (3:1), (1:1), true, bnTimeConst}(branch3x3_i)
        branch3x3   = Splice((branch3x3_1:branch3x3_2), axis=3)

        # Double 3x3 Convolution
        branch3x3dbl_i = Sequential(
            ConvBNReLULayer{384, (1:1), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{448, (1:3), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{512, (3:1), (1:1), true, bnTimeConst}
        )(x)

        branch3x3dbl_1 = ConvBNReLULayer{256, (1:3), (1:1), true, bnTimeConst}(branch3x3dbl_i)
        branch3x3dbl_2 = ConvBNReLULayer{256, (3:1), (1:1), true, bnTimeConst}(branch3x3dbl_i)
        branch3x3dbl   = Splice((branch3x3dbl_1:branch3x3dbl_2), axis=3)

        # Average Pooling
        branch_pool = Sequential(
            AveragePoolingLayer{(3:3), stride = (1:1), pad = true} :
            ConvBNReLULayer{256, (1:1), (1:1), true, bnTimeConst}
        ) (x)

        out = Splice((branch1x1:branch3x3:branch3x3dbl:branch_pool), axis=3)
    }.out
}.apply

#
# Figure 7 from https://arxiv.org/pdf/1602.07261v2.pdf
#
ReductionA {k, l, m, n, bnTimeConst} = {
    apply(x) = {
        # 3x3 Convolution
        branch3x3 = Sequential( 
            ConvBNReLULayer{n, (3:3), (2:2), false, bnTimeConst}
        ) (x)

        # Double 3x3 Convolution
        branch3x3dbl = Sequential(
            ConvBNReLULayer{k, (1:1), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{l, (3:3), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{m, (3:3), (2:2), false, bnTimeConst}
        ) (x)

        # Max Pooling
        branch_pool = MaxPoolingLayer{(3:3), stride = (2:2), pad = false}(x)

        out = Splice((branch3x3:branch3x3dbl:branch_pool), axis=3)
    }.out
}.apply

#
# Figure 8 from https://arxiv.org/pdf/1602.07261v2.pdf
#
ReductionB {bnTimeConst} = {
    apply(x) = {
        # 3x3 Convolution
        branch3x3 = Sequential( 
            ConvBNReLULayer{192, (1:1), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{192, (3:3), (2:2), false, bnTimeConst}
        ) (x)

        # 7x7 3x3 Convolution
        branch7x7_3x3 = Sequential(
            ConvBNReLULayer{256, (1:1), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{256, (1:7), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{320, (7:1), (1:1), true, bnTimeConst} :
            ConvBNReLULayer{320, (3:3), (2:2), false, bnTimeConst}
        )(x)

        # Max Pooling
        branch_pool = MaxPoolingLayer{(3:3), stride = (2:2), pad = false}(x)

        out = Splice((branch3x3:branch7x7_3x3:branch_pool), axis=3)
    }.out
}.apply